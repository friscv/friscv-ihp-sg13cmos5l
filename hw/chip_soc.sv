// Copyright 2026 FER, HPC Architecture and Application Research Center
// SPDX-License-Identifier: Apache-2.0 WITH SHL-2.1
//
// Licensed under the Solderpad Hardware License v 2.1 (the "License");
// you may not use this file except in compliance with the License, or,
// at your option, the Apache License version 2.0.
// You may obtain a copy of the License at https://solderpad.org/licenses/SHL-2.1/
//
// Emil Popovic <mail@emilpopovic.me>
// Matej Jurasic <matej.jurasic@cappig.dev>

module chip_soc import vernii_pkg::*; #(
    parameter int unsigned OcmBase           = 32'h0000_0000,
    parameter int unsigned OcmSize           = 32'h0000_2000,
    parameter int unsigned MemBase           = 32'h8000_0000,
    parameter int unsigned MemSize           = 32'h0200_0000,
    parameter int unsigned MemChips          = 2,
    parameter int unsigned LineBytes         = 64,
    parameter int unsigned Ways              = 4,
    parameter bit          SramTags          = 1'b1,
    parameter bit          HyperClockDelayed = 1'b1,
    parameter int unsigned NumGpios          = 8,
    parameter int unsigned BootSelW          = 2,
    parameter int unsigned HeartbeatDivW     = 27,
    parameter bit          HaltOnEnd         = 1'b0
) (
    input  logic  clk_i,
    input  logic  rst_ni,
`ifdef TARGET_XILINX
    input  logic  clk_ref200_i,  // IDELAYCTRL reference for the HyperBus delay lines
`endif

    output logic  heartbeat_o,

    output logic  end_o,

    // UART0
    input  logic  uart0_rx_i,
    output logic  uart0_tx_o,

    // JTAG
    input  logic  jtag_tck_i,
    input  logic  jtag_tms_i,
    input  logic  jtag_trst_ni,
    input  logic  jtag_tdi_i,
    output logic  jtag_tdo_o,
    output logic  jtag_tdo_oe_o,

    // QSPI0
    output logic       qspi0_sck_o,
    output logic [2:0] qspi0_cs_o,
    input  logic [3:0] qspi0_sd_i,
    output logic [3:0] qspi0_sd_o,
    output logic [3:0] qspi0_sd_oe_o,

    // HyperBus
    input  logic [7:0]          hyper_dq_i,
    output logic [7:0]          hyper_dq_o,
    output logic                hyper_dq_oe_o,
    input  logic                hyper_rwds_i,
    output logic                hyper_rwds_o,
    output logic                hyper_rwds_oe_o,
    output logic                hyper_ck_o,
    output logic [MemChips-1:0] hyper_cs_no,
    output logic                hyper_reset_no,

    // Boot mode select
    input  logic [BootSelW-1:0] boot_sel_i,

    // GPIO Port A
    input  logic [NumGpios-1:0] gpio_a_i,
    output logic [NumGpios-1:0] gpio_a_o,
    output logic [NumGpios-1:0] gpio_a_oe_o
);

logic [HeartbeatDivW-1:0] heartbeat_cnt;
always_ff @(posedge clk_i or negedge rst_ni) begin
    if (!rst_ni) heartbeat_cnt <= '0;
    else         heartbeat_cnt <= heartbeat_cnt + 1;
end
assign heartbeat_o = heartbeat_cnt[HeartbeatDivW-1];

localparam int unsigned HyperCfgSlv  = 0;
localparam int unsigned NumMRegRules = 1;

localparam logic [31:0] HyperCfgBaseAddr = 32'h5001_0000;
localparam logic [31:0] HyperCfgSize     = 32'h0000_1000;

localparam axi_pkg::xbar_rule_32_t [NumMRegRules-1:0] MRegRules = '{
    '{ idx: HyperCfgSlv, start_addr: HyperCfgBaseAddr, end_addr: HyperCfgBaseAddr + HyperCfgSize }
};

logic soc_rstn;

// Replicate reset for HyperBus PHY and system to avoid creating a large reset tree
logic [1:0] hyper_rstn_rep;

for (genvar i = 0; i < 2; i++) begin : gen_hyper_rst_rep
    soc_rst_replica i_soc_rst_replica (
        .clk_i,
        .rst_ni ( soc_rstn          ),
        .rst_no ( hyper_rstn_rep[i] )
    );
end

vernii_axi_req_t  axi_mem_req;
vernii_axi_resp_t axi_mem_rsp;

vernii_reg_req_t [NumMRegRules-1:0] m_reg_req;
vernii_reg_rsp_t [NumMRegRules-1:0] m_reg_rsp;

logic [31:0] gpio_a_in, gpio_a_out, gpio_a_oe;

assign gpio_a_in      = 32'(gpio_a_i);
assign gpio_a_o     = gpio_a_out[NumGpios-1:0];
assign gpio_a_oe_o  = gpio_a_oe [NumGpios-1:0];

`pragma diagnostic push
`pragma diagnostic ignore="-Wempty-output-connection"
vernii_soc #(
    .OcmBase        ( OcmBase            ),
    .OcmSize        ( OcmSize            ),
    .ExtBase        ( MemBase            ),
    .ExtSize        ( MemSize * MemChips ),
    .LineBytes      ( LineBytes          ),
    .Ways           ( Ways               ),
    .SramTags       ( SramTags           ),
    .BootSelW       ( BootSelW           ),
    .NumMRegRules   ( NumMRegRules       ),
    .MRegRules      ( MRegRules          ),
    .HaltOnEnd      ( HaltOnEnd          )
) i_vernii_soc (
    .clk_i,
    .rst_ni,
    .test_mode_i    ( 1'b0         ),
    .por_rst_no     ( /* unused */ ),
    .soc_rst_no     ( soc_rstn     ),
    .end_o,
    .s_axi_gp_req_i ( '0           ),
    .s_axi_gp_rsp_o ( /* unused */ ),
    .m_axi_hp_req_o ( axi_mem_req  ),
    .m_axi_hp_rsp_i ( axi_mem_rsp  ),
    .m_reg_req_o    ( m_reg_req    ),
    .m_reg_rsp_i    ( m_reg_rsp    ),
    .boot_sel_i,
    .uart0_rx_i,
    .uart0_tx_o,
    .uart0_cts_ni   ( 1'b0         ),
    .uart0_dsr_ni   ( 1'b0         ),
    .uart0_dcd_ni   ( 1'b0         ),
    .uart0_rin_ni   ( 1'b0         ),
    .uart0_out1_no  ( /* unused */ ),
    .uart0_out2_no  ( /* unused */ ),
    .uart0_rts_no   ( /* unused */ ),
    .uart0_dtr_no   ( /* unused */ ),
    .jtag_tck_i,
    .jtag_tms_i,
    .jtag_trst_ni,
    .jtag_tdi_i,
    .jtag_tdo_o,
    .jtag_tdo_oe_o,
    .qspi0_sck_o,
    .qspi0_sck_oe_o ( /* unused */ ),  // tied high inside spi_host
    .qspi0_cs_o,
    .qspi0_cs_oe_o  ( /* unused */ ),  // tied high inside spi_host
    .qspi0_sd_o,
    .qspi0_sd_oe_o,
    .qspi0_sd_i,
    .ext_irq_i      ( '0           ),
    .gpio_a_i       ( gpio_a_in    ),
    .gpio_a_o       ( gpio_a_out   ),
    .gpio_a_oe_o    ( gpio_a_oe    )
);
`pragma diagnostic pop

/////////////////////
// External Memory //
/////////////////////

localparam int unsigned HyperNumPhys    = 1;
localparam int unsigned HyperMinFreqMHz = 40;

// Requests past the mask alias onto low addresses instead of faulting; one
// clock between CS# and CK leaves tDSV margin for the latency RWDS sample
function automatic hyperbus_pkg::hyper_cfg_t hyper_rst_cfg();
    hyper_rst_cfg = hyperbus_pkg::gen_RstCfg(HyperNumPhys, HyperMinFreqMHz);
    hyper_rst_cfg.address_mask_msb = 5'($clog2(MemSize));
    hyper_rst_cfg.csn_to_ck_cycles = 4'd1;
endfunction

`pragma diagnostic push
`pragma diagnostic ignore="-Wempty-output-connection"
hyperbus #(
    .NumChips        ( MemChips                ),
    .NumPhys         ( HyperNumPhys            ),
    .RstCfg          ( hyper_rst_cfg()         ),
    .UsePhyClkDivider( !HyperClockDelayed      ),
    .AxiAddrWidth    ( AddrWidth               ),
    .AxiDataWidth    ( DataWidth               ),
    .AxiIdWidth      ( AxiIdWidth              ),
    .AxiUserWidth    ( AxiUserWidth            ),
    .axi_req_t       ( vernii_axi_req_t        ),
    .axi_rsp_t       ( vernii_axi_resp_t       ),
    .axi_w_chan_t    ( vernii_axi_w_chan_t     ),
    .axi_b_chan_t    ( vernii_axi_b_chan_t     ),
    .axi_ar_chan_t   ( vernii_axi_ar_chan_t    ),
    .axi_r_chan_t    ( vernii_axi_r_chan_t     ),
    .axi_aw_chan_t   ( vernii_axi_aw_chan_t    ),
    .RegAddrWidth    ( 32                      ),
    .RegDataWidth    ( 32                      ),
    .reg_req_t       ( vernii_reg_req_t        ),
    .reg_rsp_t       ( vernii_reg_rsp_t        ),
    .axi_rule_t      ( axi_pkg::xbar_rule_32_t ),
    .MinFreqMHz      ( HyperMinFreqMHz         ),
    .RstChipBase     ( MemBase                 ),
    .RstChipSpace    ( MemSize                 ),
    .RxFifoLogDepth  ( 2                       ),
    .TxFifoLogDepth  ( 2                       ),
    .AxiLogDepth     ( 1                       ),
    .AxiWLogDepth    ( 1                       )
) i_hyperbus (
    .clk_phy_i       ( clk_i                    ),
`ifdef TARGET_XILINX
    .clk_ref200_i,
`endif
    .rst_phy_ni      ( hyper_rstn_rep[0]        ),
    .clk_sys_i       ( clk_i                    ),
    .rst_sys_ni      ( hyper_rstn_rep[1]        ),
    .test_mode_i     ( 1'b0                     ),
    .axi_req_i       ( axi_mem_req              ),
    .axi_rsp_o       ( axi_mem_rsp              ),
    .reg_req_i       ( m_reg_req[HyperCfgSlv]   ),
    .reg_rsp_o       ( m_reg_rsp[HyperCfgSlv]   ),
    .hyper_cs_no,
    .hyper_ck_o,
    .hyper_ck_no     ( /* unused */             ),  // single-ended for 3.0 V parts
    .hyper_rwds_o,
    .hyper_rwds_i,
    .hyper_rwds_oe_o,
    .hyper_dq_i,
    .hyper_dq_o,
    .hyper_dq_oe_o,
    .hyper_reset_no
);
`pragma diagnostic pop

endmodule
